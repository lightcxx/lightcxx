macro(add_lightcxx_test_common_options target)
    AddTargetCompileWarnings(${target})
    target_link_libraries(${target} PUBLIC lightcxx_testing)
    set_target_properties(${target} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/tests)
    if (COMPILE_OPTIONS)
        target_compile_options(${target} PUBLIC ${COMPILE_OPTIONS})
    endif ()
endmacro()

macro(add_lightcxx_compile_fail_test target name message)
    set_target_properties(${target} PROPERTIES
            EXCLUDE_FROM_ALL TRUE
            EXCLUDE_FROM_DEFAULT_BUILD TRUE)
    if (NOT SKIP_TEST)
        add_test(NAME ${name}
                COMMAND ${CMAKE_COMMAND}
                --build .
                --target ${target}
                --config $<CONFIGURATION>
                WORKING_DIRECTORY ${CMAKE_BINARY_DIR})
        set_tests_properties(${test_name} PROPERTIES WILL_FAIL TRUE)
        if (message)
            set_tests_properties(${test_name} PROPERTIES PASS_REGULAR_EXPRESSION "${message}")
        endif ()
    endif ()
endmacro()

function(add_lightcxx_test filename section name)
    # Parse expectations from test file comments.
    file(STRINGS ${filename} lines)
    set(EXIT "CODE = 0")
    set(STEPS "")
    set(NO_COMPILE "")
    foreach (line ${lines})
        if (NOT (line MATCHES "^//[\ ]*([A-Z_]*):([A-Z_]*) (.*)$"))
            break()
        endif ()
        set(keyword ${CMAKE_MATCH_1})
        set(argument ${CMAKE_MATCH_2})
        if ("${keyword}" STREQUAL "EXPECT" AND (("${argument}" STREQUAL "NO_COMPILE") OR ("${argument}" STREQUAL "EXIT")))
            set(${argument} "${CMAKE_MATCH_3}")
        elseif ("${keyword}" STREQUAL "EXPECT" AND "${argument}" STREQUAL "STEPS")
            string(LENGTH "${CMAKE_MATCH_3}" STEPS_LEN)
            math(EXPR STEPS_LEN "${STEPS_LEN} - 2")
            string(SUBSTRING "${CMAKE_MATCH_3}" 1 ${STEPS_LEN} RAW_STEPS)
            foreach (step ${RAW_STEPS})
                string(APPEND STEPS "STEP: ${step}\n")
            endforeach ()
        elseif ("${keyword}" STREQUAL "REQUEST" AND "${argument}" STREQUAL "COMPILE_OPTIONS")
            string(LENGTH "${CMAKE_MATCH_3}" COMPILE_OPTIONS_LEN)
            math(EXPR COMPILE_OPTIONS_LEN "${COMPILE_OPTIONS_LEN} - 2")
            string(SUBSTRING "${CMAKE_MATCH_3}" 1 ${COMPILE_OPTIONS_LEN} COMPILE_OPTIONS)
        elseif ("${keyword}" STREQUAL "REQUEST" AND "${argument}" STREQUAL "SKIP")
            string(LENGTH "${CMAKE_MATCH_3}" REASON_LEN)
            math(EXPR REASON_LEN "${REASON_LEN} - 2")
            string(SUBSTRING "${CMAKE_MATCH_3}" 1 ${REASON_LEN} REASON)
            message(STATUS "Section [${section}] '${name}' ${REASON}")
            set(SKIP_TEST TRUE)
        endif ()
    endforeach ()

    if (NOT (NO_COMPILE STREQUAL ""))
        if (NO_COMPILE MATCHES "^[0-9]+$")
            foreach (fail_index RANGE ${NO_COMPILE})
                set(test_name ${section}.${name}.COMPILE_FAIL_${fail_index})
                set(test_exe_name ${section}.${name}.${fail_index})
                add_library(${test_exe_name} OBJECT ${filename})
                add_lightcxx_test_common_options(${test_exe_name})
                target_compile_definitions(${test_exe_name} PUBLIC NC_TEST_ID=${fail_index})
                add_lightcxx_compile_fail_test(${test_exe_name} ${test_name} "")
            endforeach ()
        else ()
            set(test_name ${section}.${name}.COMPILE_FAIL)
            set(test_exe_name ${section}.${name})
            add_library(${test_exe_name} OBJECT ${filename})
            add_lightcxx_test_common_options(${test_exe_name})
            add_lightcxx_compile_fail_test(${test_exe_name} ${test_name} "${NO_COMPILE}")
        endif ()
        return()
    endif ()

    set(test_name ${section}.${name})
    set(test_exe_name ${section}.${name})
    set(exclude_from_all "")
    if (SKIP_TEST)
        set(exclude_from_all EXCLUDE_FROM_ALL)
    endif ()
    add_executable(${test_exe_name} ${exclude_from_all} ${filename})
    add_lightcxx_test_common_options(${test_exe_name})
    if (NOT SKIP_TEST)
        add_test(NAME ${test_name} COMMAND ${test_exe_name})
        if (NOT (EXIT STREQUAL "CODE = 0") OR STEPS)
            set_tests_properties(${test_name} PROPERTIES
                    PASS_REGULAR_EXPRESSION "^${STEPS}\n----\nPROCESS EXIT ${EXIT}"
                    ENVIRONMENT "LIGHTCXX_NEEDS_FORK=1")
        endif ()
        set_tests_properties(${test_name} PROPERTIES FAIL_REGULAR_EXPRESSION "EXPECTATION FAILED")
    endif ()
endfunction()

function(scan_for_lightcxx_tests directory)
    file(GLOB_RECURSE test_files ${directory}/**/*.cpp)
    foreach (test_file ${test_files})
        if (test_file MATCHES "^.*/([a-z.]*)/([^/]*).cpp$")
            if (CMAKE_MATCH_2 STREQUAL "skipped")
                message(STATUS "Section [${CMAKE_MATCH_1}] needs tests")
            elseif (NOT (CMAKE_MATCH_2 STREQUAL "nothing_to_do"))
                add_lightcxx_test(${test_file} ${CMAKE_MATCH_1} ${CMAKE_MATCH_2})
            endif ()
        else ()
            message(WARNING "Test path not recognized: ${test_file}")
        endif ()
    endforeach ()
endfunction()
